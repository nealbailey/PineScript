// Baileysoft Solutions Â© 2016-2026 
// Author: Neal Bailey 
//         https://www.journeymaninvestor.com/
//         https://www.tradingview.com/u/nealosis/ 
//         https://www.youtube.com/channel/UCWLOB6VrnIHc1QBMpCXUi4A/
//
// This source code is subject to the terms of the Mozilla Public License 2.0
// https://mozilla.org/MPL/2.0/
//
// ---------------------------------------------------------------------------
// This is a bullish strategy that trades breakouts. It buys when the share price crosses
// over the median-middle bollinger band and sells when the price touches the upper bollinger band. 
// The easiest way to trade this strategy is to simply buy shares when the signal is triggered. 
// The most profitable method would be to buy an in-the-money calls/puts around the 70-80 delta. 
// Or you could use a defined risk entry such as a put credit spread and collect the premium. 
// ---------------------------------------------------------------------------
//@version=5
strategy(title='Bollinger Breakout (Baileysoft)', overlay=true, initial_capital=10000)

// --------------------
// Strategy Rules
// --------------------
// ** Set chart to daily bars **
//
// 1. ETF must be trading above its 200-day simple moving average
// 2. Enter trade when price closes above the middle band
// 3. Exit trade when price touches the top band
//
// Prefer 70-80 delta in-the-money call contracts
// Do not take a new position anytime the ETF closes under the 200-day MA
// 

// Chart Properties
testStartYear = input(2017, 'Backtest Start Year')
testStartMonth = input(01, 'Backtest Start Month')
testStartDay = input(1, 'Backtest Start Day')
testPeriodStart = timestamp(testStartYear, testStartMonth, testStartDay, 0, 0)

testStopYear = input(2030, 'Backtest Stop Year')
testStopMonth = input(12, 'Backtest Stop Month')
testStopDay = input(30, 'Backtest Stop Day')
testPeriodStop = timestamp(testStopYear, testStopMonth, testStopDay, 0, 0)

// A switch to control background coloring of the test period
testPeriodBackground = input(title='Color Background?', defval=true)
testPeriodBackgroundColor = testPeriodBackground and time >= testPeriodStart and time <= testPeriodStop ? #6c6f6c : na
colorItem = color.new(testPeriodBackgroundColor, transp = 94)
bgcolor(colorItem)

// User provided values
smaLength = input(title='SMA Length', defval=20)  // Middle band period length (moving average)
stdLength = input(title='StdDev Length', defval=20)  // Range to apply bands to
ubOffset = input.float(title='Upper Band Offset', defval=2.0, step=0.5)  // Number of standard deviations above MA
lbOffset = input.float(title='Lower Band Offset', defval=2.0, step=0.5)  // Number of standard deviation below MA

testPeriod() =>
    time >= testPeriodStart and time <= testPeriodStop ? true : false

smaValue = ta.sma(close, smaLength)  // Middle band
stdDev = ta.stdev(close, stdLength)  // Standard deviation
upperBand = smaValue + stdDev * ubOffset  // Top band
lowerBand = smaValue - stdDev * lbOffset  // Bottom band
longSMA = ta.sma(close, 200)  // 200-day moving average

// Plot bands to chart
plot(series=smaValue, title='SMA', color=color.new(color.teal, 0))
plot(series=upperBand, title='UB', color=color.new(color.green, 0), linewidth=2)
plot(series=lowerBand, title='LB', color=color.new(color.red, 0), linewidth=2)

// Entry criteria (uptrending and crossing median band)
longCondition = ta.crossover(close, smaValue) and close > longSMA
// Exit condition (touching the upper band)
closeLongCondition = high >= upperBand -1 // Just below top band

if longCondition and testPeriod()
    strategy.entry(id='BUY', direction=strategy.long, qty=10)

if closeLongCondition and testPeriod()
    strategy.close(id='BUY')
