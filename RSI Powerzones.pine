// Baileysoft Solutions Â© 2016-2026 
// Author: Neal Bailey 
//         https://www.journeymaninvestor.com/
//         https://www.tradingview.com/u/nealosis/ 
//         https://www.youtube.com/channel/UCWLOB6VrnIHc1QBMpCXUi4A/
//
// This source code is subject to the terms of the Mozilla Public License 2.0
// https://mozilla.org/MPL/2.0/
//
// ---------------------------------------------------------------------------
// This is a bullish strategy using the Relative Strength Indicator (RSI). 
// It buys when the RSI indicator value closes below 30 and it sells when the
// RSI indicator closes above 55. 
//
// The easiest way to trade this strategyis to buy shares when the buy signal is triggered and 
// sell when the close signal is triggered. 
//
// The most profitable method would be to buy an in-the-money calls/puts around the 70-80 delta. 
// Or you could use a defined risk entry such as a put credit spread and collect the premium. 
// ---------------------------------------------------------------------------
//@version=5
strategy('RSI Powerzones', overlay=true, initial_capital=10000, process_orders_on_close = true)

// --------------------
// Strategy Rules
// --------------------
// ** Set chart to daily bars **
//
// 1. $SPY must be trading above its 200-day simple moving average. 
// 2. When the 4-period RSI of $SPY closes under 30 buy $SPY on the close.
// 3. Buy a second unit if the 4-period RSI closes below 25 at any time while the position is open (doubling your position).
// 4. Sell when the 4-period RSI closes above 55
//
// Prefer 70-80 delta in-the-money call contracts

// User provided values
testStartYear = input(2004, 'Backtest Start Year')
testStartMonth = input(01, 'Backtest Start Month')
testStartDay = input(1, 'Backtest Start Day')
testPeriodStart = timestamp(testStartYear, testStartMonth, testStartDay, 0, 0)

testStopYear = input(2030, 'Backtest Stop Year')
testStopMonth = input(12, 'Backtest Stop Month')
testStopDay = input(30, 'Backtest Stop Day')
testPeriodStop = timestamp(testStopYear, testStopMonth, testStopDay, 0, 0)

// A switch to control background coloring of the test period
testPeriodBackground = input(title='Color Background?', defval=true)
testPeriodBackgroundColor = testPeriodBackground and time >= testPeriodStart and time <= testPeriodStop ? #6c6f6c : na
colorItem = color.new(testPeriodBackgroundColor, transp = 94)
bgcolor(colorItem)

// Entry and exit RSI readings
RSIEntry = input(30, 'RSI Entry')
RSIExit = input(55, 'RSI Exit')
RSILength = input(4, 'RSI Length')

// Determine if current bar is within range
// @returns boolean true or false
testPeriod() =>
    time >= testPeriodStart and time <= testPeriodStop ? true : false

longSMA = ta.sma(close, 200)  // 200-day moving average
rsi = ta.rsi(close, RSILength)  // relative strength indicator with custom length

longCondition = close > longSMA and rsi <= RSIEntry  // Enter
closeLongCondition = rsi >= RSIExit // Exit

if longCondition and testPeriod()
    strategy.entry('BUY', strategy.long, 10)

if closeLongCondition and testPeriod()
    strategy.close('BUY')
